plugins {
    id "org.dm.bundle" version "0.8.4"
}

apply plugin: 'java'

description = 'Daikon main shared library'

dependencies {
    compile group: 'org.apache.avro', name: 'avro', version: '1.8.0'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.5.3'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.5.3'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.6'
    compile group: 'com.cedarsoftware', name: 'json-io', version: '4.4.1-SNAPSHOT'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.2.3'
}

jar {
    manifest {
        name "${project.getName()} Library"
        attributes 'Bundle-SymbolicName': 'org.talend.daikon.lib'
    }
}

// Applies the bundle plugin to a Jar task
void applyPlugin(Task jarTask) {
    def jarBuilderFactory = new org.dm.gradle.plugins.bundle.JarBuilderFactoryDecorator(
            jarTask, project.bundle.jarBuilderFactory)

    jarTask.deleteAllActions()
    jarTask.doLast(new org.dm.gradle.plugins.bundle.BundleGenerator(jarBuilderFactory))
    jarTask.manifest = new org.dm.gradle.plugins.bundle.ManifestSubstitute(jarBuilderFactory, jarTask.manifest)
}

task bundleJar(type: Jar) {
    from sourceSets.main.output
    classifier 'bundle'

    applyPlugin(bundleJar)

    manifest {
        attributes 'Bundle-Name': "${project.getName()} Bundle",
                'Bundle-SymbolicName': 'org.talend.daikon.lib',
                'Dynamic-ImportPackage': '*',
                'Service-Component': '*',
                'Embed-Transitive': 'true',
                'Embed-Dependency': '*;artifactId=paranamer|snappy-java|commons-compress|xz;inline=true'
    }
}

task testJar(type: Jar) {
    from sourceSets.test.output
    classifier 'tests'

    applyPlugin(bundleJar)

    manifest {
        attributes 'Bundle-Name': "${project.getName()} Test",
                'Bundle-SymbolicName': 'org.talend.daikon.test',
                'Dynamic-ImportPackage': '*',
                //'Conditional-Package': 'junit*|org.*',
                'Private-Package': 'junit*|org.*|com.*'
               // 'Service-Component': '*'
                //'Embed-Transitive': 'true',
              //  'Embed-Dependency': '*;scope=test;artifactId=!hamcrest-core|junit'
    }
}
jar.dependsOn bundleJar, testJar
